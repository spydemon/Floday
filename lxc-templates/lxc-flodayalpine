#!/usr/bin/env perl

use v5.010;

use Backticks;
use File::Slurp;
use Getopt::Long;

$Backticks::autodie = 1;

my $CACHE_FOLDER = '/tmp/floday/lxc-flodayalpine';

my ($path, $name, $rootfs);
GetOptions("path=s" => \$path,
  "name=s" => \$name,
  "rootfs=s" => \$rootfs
);

my %templateArguments= (@ARGV);

sub deployAPKIndex {
	if (!-d "$CACHE_FOLDER/apkIndex") {
		my $APKArchive = getFile('APKINDEX.tar.gz');
		`mkdir -p $CACHE_FOLDER/apkIndex`;
		`tar -xf $APKArchive -C $CACHE_FOLDER/apkIndex`;
	}
	return "$CACHE_FOLDER/apkIndex/APKINDEX";
}

sub generateLXCConfiguration{
	#TODO : Apply restriction by apparmor and seccomp.
	#TODO : randomize (u|g)id but check for conflicts.
	my $id = getUserId();
	open($configurationFile, '>', "$path/config");
	my @configurationEntries = (
	  "lxc.mount.entry = $CACHE_FOLDER/apk $rootfs/etc/apk/cache none bind 0.0",
	  "lxc.mount.auto = proc:mixed",
	  "lxc.rootfs = $rootfs",
	  "lxc.id_map = u 0 $id 100",
	  "lxc.id_map = g 0 $id 100"
	);
	say{$configurationFile} $_ foreach @configurationEntries;
}

sub getAlpineVersionToUse {
	'v' . $templateArguments{version} if $templateArguments{version} =~ /^[0-9]{1,2}\.[0-9]{1}$/
	  or die "The version of alpine to use is mandatory";
}

sub getArchitectureToUse {
	$templateArguments{architecture} // 'x86_64';
}

sub getFile {
	my ($packageName) = @_;
	my $repositoryAddress = getMainRepositoryAddress() . '/' . getArchitectureToUse();
	my $pathForDownloadedFile = "$CACHE_FOLDER/files";
	if (!-f "$pathForDownloadedFile/$packageName") {
		`wget -q -P $pathForDownloadedFile $repositoryAddress/$packageName`;
	}
	return "$pathForDownloadedFile/$packageName";
}

sub getPackage {
	my ($target) = @_;
	my $index = read_file(deployAPKIndex());
	while (
	  $index =~ /
	    ^P:(?P<name>.*)\n		#Get the next software name present in the index.
	    ^V:(?P<version>.*)\n	#Get the next software version present in the index.
	  /mgx
	) {
		if ($+{name} eq $target) {
			return getFile("$+{name}-$+{version}.apk");
		}
	}
	die("Package $target was not found in APKIndex");
}

sub getUserId {
	#TODO: manage collisions
	state $id = int(rand(1000)+1) * 100 + 100000;
}

sub getMainRepositoryAddress {
	'https://wiki.alpinelinux.org/cgi-bin/dl.cgi/' . getAlpineVersionToUse() . '/main/';
}

sub initSystem {
	my $apkRepoToUse = getMainRepositoryAddress();
	my $arch = getArchitectureToUse();
	`mkdir -p $CACHE_FOLDER/apk && ln -s $CACHE_FOLDER/apk $rootfs/etc/apk/cache `;
	`echo $apkRepoToUse > $rootfs/etc/apk/repositories`;
	#TODO: update of repo only when needed.
	open (my $fh, '-|', "$rootfs/sbin/apk.static -U add --initdb --root $rootfs --arch $arch alpine-base");
	print $_ while(<$fh>);
}

sub installPackage {
	my ($packageName) = @_;
	my $packagePath = getPackage($packageName);
	`tar -xf '$packagePath' -C '$rootfs' 2>/dev/null`;
	$packageName =~ s/\W//g;
	if (defined &{"_installPackage_$packageName"}) {
		no strict 'refs';
		&{"_installPackage_$packageName"}();
	}
}

sub manageSystemConfiguration {
	my $id = getUserId();
	`rm $rootfs/etc/apk/cache && mkdir -p $rootfs/etc/apk/cache`;
	#TODO : resolv.conf should be provided in container instantation.
	`cp /etc/resolv.conf $rootfs/etc/resolv.conf`;
	`chmod 750 $path`;
	`chown :$id $path`;
	`chown -R $id:$id $rootfs`;
}

sub _installPackage_alpinekeys {
	my %keys = (
	  'alpine-devel@lists.alpinelinux.org-4a6a0840.rsa.pub' => '9c102bcc376af1498d549b77bdbfa815ae86faa1d2d82f040e616b18ef2df2d4',
	  'alpine-devel@lists.alpinelinux.org-4d07755e.rsa.pub' => '2adcf7ce224f476330b5360ca5edb92fd0bf91c92d83292ed028d7c4e26333ab',
	  'alpine-devel@lists.alpinelinux.org-5243ef4b.rsa.pub' => 'ebf31683b56410ecc4c00acd9f6e2839e237a3b62b5ae7ef686705c7ba0396a9',
	  'alpine-devel@lists.alpinelinux.org-524d27bb.rsa.pub' => '1bb2a846c0ea4ca9d0e7862f970863857fc33c32f5506098c636a62a726a847b',
	  'alpine-devel@lists.alpinelinux.org-5261cecb.rsa.pub' => '12f899e55a7691225603d6fb3324940fc51cd7f133e7ead788663c2b7eecb00c'
	);
	my $keyList = `echo $rootfs/etc/apk/keys/*.pub`;
	my $keyChecked = 0;
	while ($keyList =~ /
	    (?P<keyPathToUse>.*?                    #keyPathToUse will contain the absolute path of the next key.
	      (?P<keyNameToUse>alpine-devel.*?)     #keyNameToUse will contain only the name of the next key.
	    )
	  \s/gx
	) {
		`cd $rootfs/etc/apk/keys && echo "$keys{$+{keyNameToUse}} $+{keyNameToUse}" | sha256sum -c -`;
		$keyChecked += 1;
	}
	die ('Too much key signatures are in this template') if ($keyChecked != keys %keys);
}

sub _installPackage_apktoolsstatic {
	`echo $rootfs/sbin/apk.static.*.pub`         #Get the path of the file corresponding to apk.static signed hash.
	  =~ /
	    (?P<signatureToCheck>^.*                 #Capture the absolute path without the \n at the end of the line.
	      (?P<keyToUse>alpine-devel[^\/]*)       #The name of the key used for the signature is present in the hash name.
	    )
	  \s$/x;
	my $fileToCheck = "$rootfs/sbin/apk.static";
	my $keyToUseForChecking = "$rootfs/etc/apk/keys/$+{keyToUse}";
	`openssl dgst -sha1 -verify $keyToUseForChecking -signature $+{signatureToCheck} $fileToCheck`;
}

installPackage('alpine-keys');
installPackage('apk-tools-static');
initSystem();
generateLXCConfiguration();
manageSystemConfiguration();
